plugins {
  id 'java'
}

group 'space.nerdsin.plugins'
version '1.0'

sourceCompatibility = 1.8

subprojects {
  ext {
    jooqGradleVersion = '3.0.3'
    jooqVersion = '3.12.1'

    h2DatabaseVersion = '1.4.200'
    flywayCoreVersion = '6.1.4'
    
    lombokVersion = '1.18.10'
    guiceVersion = '4.2.2'
    reflectionsVersion = '0.9.11'
  }
  
  apply plugin: 'java'
  
  repositories {
    mavenCentral()
    maven {
      url 'https://papermc.io/repo/repository/maven-public/'
    }
  }
  
  task copyLibraryJars(type: Copy) {
    from configurations.compile
    into rootProject.file('mcserver/plugins/libs')
  }
  
  build.finalizedBy copyLibraryJars
  
  afterEvaluate {
    dependencies {
      implementation group: 'com.destroystokyo.paper', name: 'paper-api', version: '1.13.2-R0.1-SNAPSHOT'
    }
  
    jar {
      manifest {
        attributes(
            "Class-Path": configurations.compile.collect { "libs/${it.getName()}" }.join(' '))
      }
      destinationDir rootProject.file('mcserver/plugins')
    }
    
    processResources {
      filesMatching('**/plugin.yml') {
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
            'name'      : project.hasProperty('pretty-name')
                ? project.property('pretty-name') : project.name,
            'class'     : project.property('main'),
            'version'   : project.version,
            'apiversion': project.property('apiversion')
        ]);
      }
    }
  }
}

sourceSets {}

gradle.buildFinished {
  project.buildDir.deleteDir()
}

task copyPluginJars(type: Copy, dependsOn: subprojects.jar) {
  from subprojects.jar
  into project.file('mcserver/plugins')
}

task createServerDirs() {
  if(project.file('mcserver').mkdir()) {
    project.file('mcserver/plugins').mkdir();
    project.file('mcserver/worlds').mkdir();
  }
}
